
Процедура КоманднаяПанельКлассыОбновить(Кнопка)
	
	ОбновитьДанные();
	
КонецПроцедуры

Процедура ДействияФормыПрименить(Кнопка)
	
	Если ВыполнитьРегистрацию() Тогда 
		ОбновитьДанные(Ложь);
	КонецЕсли; 
	
КонецПроцедуры

Процедура КомпьютерПриИзменении(Элемент)
	
	ирОбщий.ПолеВводаСИсториейВыбора_ПриИзмененииЛкс(Элемент, ЭтаФорма);
	ОбновитьДанные();
	
КонецПроцедуры

Процедура КомпьютерНачалоВыбораИзСписка(Элемент, СтандартнаяОбработка)

	ирОбщий.ПолеВводаСИсториейВыбора_НачалоВыбораИзСпискаЛкс(Элемент, ЭтаФорма);
	
КонецПроцедуры

Процедура ОбновитьДанные(ЗапроситьПодтверждение = Истина)
	
	Если ЗапроситьПодтверждение И Не ЗапроситьПодтверждение() Тогда
		Возврат;
	КонецЕсли; 
	СостояниеСтрокКлассы = ирОбщий.ТабличноеПолеСостояниеСтрокЛкс(ЭлементыФормы.Классы, "ИмяКласса");
	ОбновитьТаблицуКлассов();
	ирОбщий.ТабличноеПолеВосстановитьСостояниеСтрокЛкс(ЭлементыФормы.Классы, СостояниеСтрокКлассы);
	ЭтаФорма.Модифицированность = Ложь;
	
КонецПроцедуры

Функция ЗапроситьПодтверждение()
	
	Результат = Истина;
	Если Модифицированность Тогда
		Ответ = Вопрос("Вы не применили изменения. Продолжить?", РежимДиалогаВопрос.ОКОтмена);
		Результат = Ответ = КодВозвратаДиалога.ОК;
	КонецЕсли; 
	Возврат Результат;

КонецФункции

Процедура ПриОткрытии()
	
	ирОбщий.Форма_ПриОткрытииЛкс(ЭтаФорма);
	ЭтотОбъект.ПроверочнаяСтрокаСоединения = ПроверочнаяСтрокаСоединенияПоУмолчанию();
	ЗаполнитьТипыCOMКлассов();
	КоманднаяПанельСборкиПлатформыОбновить();
	ОбновитьТаблицуКлассов();
	ЭтаФорма.ОтАдминистратора = ирКэш.ВКОбщаяЛкс().IsAdmin();
	Если ЗначениеЗаполнено(ПараметрТипКласса) Тогда
		ТипКласса = ТипыComКлассов.Найти(ПараметрТипКласса, "Имя");
		КлючПоиска = Новый Структура;
		КлючПоиска.Вставить("ИмяКласса", "V" + ирКэш.НомерИзданияПлатформыЛкс() + ТипКласса.ИмяКлассаПослеV8);
		КлючПоиска.Вставить("x64", x64Текущая);
		Найденые = Классы.НайтиСтроки(КлючПоиска);
		Если Найденые.Количество() > 0 Тогда
			ЭлементыФормы.Классы.ТекущаяСтрока = Найденые[0];
			ЭлементыФормы.Классы.ТекущаяКолонка = ЭлементыФормы.Классы.Колонки.НовыйСборкаПлатформы;
		КонецЕсли; 
	КонецЕсли; 
	
КонецПроцедуры

Процедура КлассыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ИзданиеПлатформы = ЭлементыФормы.Классы.ТекущиеДанные.ИзданиеПлатформы;
	Разрядность = ЭлементыФормы.Классы.ТекущиеДанные.x64;
	ИмяКласса = ЭлементыФормы.Классы.ТекущиеДанные.ТипКласса;
	Внутрипроцессный = ЭлементыФормы.Классы.ТекущиеДанные.Внутрипроцессный;
	СписокСборок = Новый СписокЗначений();
	Попытка
		СборкиПлатформы.НайтиСтроки(Новый Структура(ИмяКласса));
		ПолнаяПоддержкаКласса = Истина;
	Исключение
		ПолнаяПоддержкаКласса = Ложь;
	КонецПопытки;
	Если ПолнаяПоддержкаКласса Тогда
		ОтборСтрок = Новый Структура("ИзданиеПлатформы, " + ИмяКласса, ИзданиеПлатформы, Истина);
		ОтборСтрок.Вставить("x64", Разрядность);
		СтрокиСборок = СборкиПлатформы.НайтиСтроки(ОтборСтрок);
		Для Каждого СтрокаСборки Из СтрокиСборок Цикл
			СписокСборок.Добавить(ПредставлениеСборкиПлатформы(СтрокаСборки, Внутрипроцессный));
		КонецЦикла;
	КонецЕсли; 
	ЭлементыФормы.Классы.Колонки.НовыйСборкаПлатформы.ЭлементУправления.СписокВыбора = СписокСборок;

КонецПроцедуры

Процедура КлассыНовыйСборкаПлатформыПриИзменении(Элемент)
	
	ЭтаФорма.Модифицированность = Истина;
	
КонецПроцедуры

Процедура КлассыПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки) Экспорт
	
	ирОбщий.ТабличноеПолеПриВыводеСтрокиЛкс(ЭтаФорма, Элемент, ОформлениеСтроки, ДанныеСтроки);
	ОформлениеСтроки.Ячейки.Сборка.Видимость = Ложь;
	ОформлениеСтроки.Ячейки.Файл.Видимость = Ложь;
	Если СтрЗаменить(ДанныеСтроки.ИзданиеПлатформы, ".", "") = ирКэш.НомерИзданияПлатформыЛкс() Тогда 
		Если ДанныеСтроки.СборкаПлатформы = ТекущаяСборкаПлатформы Тогда
			ОформлениеСтроки.Ячейки.СборкаПлатформы.ЦветТекста = WebЦвета.Синий;
		Иначе
			ОформлениеСтроки.Ячейки.СборкаПлатформы.ЦветТекста = WebЦвета.Красный;
		КонецЕсли; 
		Если ДанныеСтроки.НовыйСборкаПлатформы = ТекущаяСборкаПлатформы Тогда
			ОформлениеСтроки.Ячейки.НовыйСборкаПлатформы.ЦветТекста = WebЦвета.Синий;
		Иначе
			ОформлениеСтроки.Ячейки.НовыйСборкаПлатформы.ЦветТекста = WebЦвета.Красный;
		КонецЕсли; 
	КонецЕсли; 
	
КонецПроцедуры

Процедура ДействияФормыФайлВключенияОтладки(Кнопка)
	
	#Если Не ТонкийКлиент И Не ВебКлиент Тогда
		Каталог = ирОбщий.КаталогНастроекПриложения1СЛкс();
	#Иначе
		Каталог = "";
	#КонецЕсли 
	ИмяФайла = ирОбщий.ВыбратьФайлЛкс(Ложь, "xml",,, Каталог, "comcntrcfg.xml");
	Если Не ЗначениеЗаполнено(ИмяФайла) Тогда
		Возврат;
	КонецЕсли; 
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла("xml");
	ТекстовыйДокумент = ирОбщий.ТекстКонфигурационногоФайлаВнешнихСоединенийЛкс();
	ТекстовыйДокумент.Записать(ИмяВременногоФайла, КодировкаТекста.ANSI);
	мПлатформа = ирКэш.Получить();
	#Если Сервер И Не Сервер Тогда
	    мПлатформа = Обработки.ирПлатформа.Создать();
	#КонецЕсли
	мПлатформа.ПереместитьФайлКакАдминистратор(ИмяВременногоФайла, ИмяФайла);
	
КонецПроцедуры

Процедура КоманднаяПанельСборкиПлатформыЗапуститьТолстый(Кнопка)
	
	ТекущаяСтрока = ЭлементыФормы.СборкиПлатформы.ТекущаяСтрока;
	#Если Сервер И Не Сервер Тогда
	    ТекущаяСтрока = СборкиПлатформы.Добавить();
	#КонецЕсли
	Если ТекущаяСтрока = Неопределено Или Не ТекущаяСтрока.Application Тогда
		Возврат;
	КонецЕсли; 
	ЗапуститьПриложение(ТекущаяСтрока.Каталог + "bin\1cv8.exe");
	
КонецПроцедуры

Процедура КоманднаяПанельСборкиПлатформыЗапуститьТонкий(Кнопка)
	
	ТекущаяСтрока = ЭлементыФормы.СборкиПлатформы.ТекущаяСтрока;
	#Если Сервер И Не Сервер Тогда
	    ТекущаяСтрока = СборкиПлатформы.Добавить();
	#КонецЕсли
	Если ТекущаяСтрока = Неопределено Или Не ТекущаяСтрока.CApplication Тогда
		Возврат;
	КонецЕсли; 
	ЗапуститьПриложение(ТекущаяСтрока.Каталог + "bin\1cv8c.exe");

КонецПроцедуры

Процедура КоманднаяПанельКлассыЗапуститьОтАдминистратора(Кнопка)
	
	ирОбщий.ЗапуститьСеансПодПользователемЛкс(ИмяПользователя(),,, "ОбычноеПриложение",,,,,,,, Истина);
	
КонецПроцедуры

Процедура КоманднаяПанельСборкиПлатформыОбновить(Кнопка = Неопределено)
	
	ирОбщий.ЗаполнитьДоступныеСборкиПлатформыЛкс(СборкиПлатформы, Компьютер, ТипыComКлассов, ЭлементыФормы.СборкиПлатформы);
	
КонецПроцедуры

Процедура КоманднаяПанельКлассыПроверитьРаботуОбъектов(Кнопка)
	
	СтрокиКлассов = Классы.НайтиСтроки(Новый Структура("X64, Зарегистрирован", x64Текущая, Истина));
	#Если Сервер И Не Сервер Тогда
		СтрокиКлассов = Классы;
	#КонецЕсли
	Индикатор = ирОбщий.ПолучитьИндикаторПроцессаЛкс(СтрокиКлассов.Количество());
	Для Каждого СтрокаКласса Из СтрокиКлассов Цикл
		ирОбщий.ОбработатьИндикаторЛкс(Индикатор);
		СтрокаКласса.ПроверкаСоздания = "";
		СтрокаКласса.ПроверкаПодключения = "";
		ТипКласса = ТипыComКлассов.Найти(СтрокаКласса.ТипКласса, "Имя");
		Попытка
			ПроверочныйОбъект = Новый COMОбъект(СтрокаКласса.ИмяКласса);
			СтрокаКласса.ПроверкаСоздания = "ОК";
			Если СтрЗаменить(СтрокаКласса.ИзданиеПлатформы, ".", "") <> ирКэш.НомерИзданияПлатформыЛкс() Тогда
				Продолжить;
			КонецЕсли; 
			Если СтрокаКласса.СборкаПлатформы <> ТекущаяСборкаПлатформы Тогда
				ВызватьИсключение "Ошибка. Зарегистрированная в COM сборка платформы отличается от текущей";
			КонецЕсли; 
			// https://www.hostedredmine.com/issues/908123
			ПроверочныйОбъект.Connect(ПроверочнаяСтрокаСоединения);
			СтрокаКласса.ПроверкаПодключения = "ОК";
			Если Не ТипКласса.Внутрипроцессный Тогда
				Попытка
					ПроверочныйОбъект.ЗавершитьРаботуСистемы(Ложь, Ложь);
				Исключение
				КонецПопытки;
			КонецЕсли; 
		Исключение
			ОписаниеОшибки = ОписаниеОшибки();
			Если Не ЗначениеЗаполнено(СтрокаКласса.ПроверкаСоздания) Тогда
				СтрокаКласса.ПроверкаСоздания = Сред(ОписаниеОшибки, Найти(ОписаниеОшибки, "}:") + 3);
			ИначеЕсли Не ЗначениеЗаполнено(СтрокаКласса.ПроверкаПодключения) Тогда
				СтрокаКласса.ПроверкаПодключения = Сред(ОписаниеОшибки, Найти(ОписаниеОшибки, "}:") + 3);
			КонецЕсли; 
		КонецПопытки; 
	КонецЦикла;
	ирОбщий.ОсвободитьИндикаторПроцессаЛкс();
	
КонецПроцедуры

Процедура СборкиПлатформыВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)
	
	ОткрытьСборкуПлатформыВПроводнике();

КонецПроцедуры

Процедура ОткрытьСборкуПлатформыВПроводнике()
	
	ВыбраннаяСтрока = ЭлементыФормы.СборкиПлатформы.ТекущаяСтрока;
	Если ВыбраннаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	Если ирОбщий.ЭтоИмяЛокальногоКомпьютераЛкс(Компьютер) И ВыбраннаяСтрока.ФайлыСуществуют Тогда
		ЗапуститьПриложение(ВыбраннаяСтрока.Каталог);
	КонецЕсли;

КонецПроцедуры

Процедура КлассыВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)
	
	Если Колонка = Элемент.Колонки.ИмяФайла Тогда
		ирОбщий.ОткрытьФайлВПроводникеЛкс(ВыбраннаяСтрока.ИмяФайла);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник) Экспорт
	
	ирОбщий.ФормаОбработкаОповещенияЛкс(ЭтаФорма, ИмяСобытия, Параметр, Источник); 
	
КонецПроцедуры

Процедура КлассыПриАктивизацииСтроки(Элемент)
	
	ирОбщий.ТабличноеПолеПриАктивизацииСтрокиЛкс(ЭтаФорма, Элемент);
	ЭтаФорма.ОписаниеКласса = "";
	Если Элемент.ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	ЭтаФорма.ОписаниеКласса = ТипыComКлассов.Найти(Элемент.ТекущаяСтрока.ТипКласса, "Имя").Описание;
	
КонецПроцедуры

Процедура СборкиПлатформыПриАктивизацииСтроки(Элемент)
	
	ирОбщий.ТабличноеПолеПриАктивизацииСтрокиЛкс(ЭтаФорма, Элемент);

КонецПроцедуры

Процедура СборкиПлатформыПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки) Экспорт
	
	ирОбщий.ТабличноеПолеПриВыводеСтрокиЛкс(ЭтаФорма, Элемент, ОформлениеСтроки, ДанныеСтроки);
	Если ДанныеСтроки.СборкаПлатформы = ТекущаяСборкаПлатформы Тогда
		ОформлениеСтроки.ЦветТекста = WebЦвета.Синий;
	КонецЕсли; 

КонецПроцедуры

Процедура КлсКомандаНажатие(Кнопка) Экспорт 
	
	ирОбщий.УниверсальнаяКомандаФормыЛкс(ЭтаФорма, Кнопка);
	
КонецПроцедуры

Процедура ВнешнееСобытие(Источник, Событие, Данные) Экспорт
	
	ирОбщий.Форма_ВнешнееСобытиеЛкс(ЭтаФорма, Источник, Событие, Данные);

КонецПроцедуры

Процедура ТабличноеПолеПриПолученииДанных(Элемент, ОформленияСтрок) Экспорт 
	
	ирОбщий.ТабличноеПолеПриПолученииДанныхЛкс(ЭтаФорма, Элемент, ОформленияСтрок);

КонецПроцедуры

Процедура ПриЗакрытии()
	
	ирОбщий.Форма_ПриЗакрытииЛкс(ЭтаФорма);

КонецПроцедуры

Процедура КоманднаяПанельСборкиПлатформыУстановитьВНовую(Кнопка)
	
	ТекущаяСтрока = ЭлементыФормы.СборкиПлатформы.ТекущаяСтрока;
	Если ТекущаяСтрока = Неопределено Или Не ТекущаяСтрока.ФайлыСуществуют Тогда
		Возврат;
	КонецЕсли; 
	УстановитьНовыеСборкиПоСтрокеДоступнойСборки(ТекущаяСтрока);
	
КонецПроцедуры

Процедура УстановитьНовыеСборкиПоСтрокеДоступнойСборки(Знач ТекущаяСтрока)
	
	Для Каждого ТипКласса Из ТипыComКлассов Цикл
		Если ТекущаяСтрока[ТипКласса.Имя] Тогда
			Отбор = Новый Структура("ИзданиеПлатформы, X64, ТипКласса");
			ЗаполнитьЗначенияСвойств(Отбор, ТекущаяСтрока);
			Отбор.ТипКласса = ТипКласса.Имя;
			Классы.НайтиСтроки(Отбор)[0].НовыйСборкаПлатформы = ТекущаяСтрока.СборкаПлатформы;
		КонецЕсли; 
	КонецЦикла;

КонецПроцедуры

Процедура КоманднаяПанельСборкиПлатформыОткрытьКаталогВПроводнике(Кнопка)
	
	ОткрытьСборкуПлатформыВПроводнике();

КонецПроцедуры

Процедура КоманднаяПанельКлассыОчиститьНовые(Кнопка)
	
	Для Каждого СтрокаКласса Из Классы Цикл
		СтрокаКласса.НовыйСборкаПлатформы = Неопределено;
	КонецЦикла;
	
КонецПроцедуры

Процедура ДействияФормыОткрытьСтатью(Кнопка)
	ЗапуститьПриложение("https://infostart.ru/1c/articles/387577/");
КонецПроцедуры

Процедура ПроверочнаяСтрокаСоединенияПриИзменении(Элемент)
	СтрокаСоедиенияСохраняемая = ПроверочнаяСтрокаСоединения;
	Пароль = ирОбщий.СтрокаМеждуМаркерамиЛкс(СтрокаСоедиенияСохраняемая, "Pwd=", ";", Ложь, Истина);
	СтрокаСоедиенияСохраняемая = СтрЗаменить(СтрокаСоедиенияСохраняемая, Пароль, "Pwd=;");
	ирОбщий.ПолеВводаСИсториейВыбора_ПриИзмененииЛкс(Элемент, ЭтаФорма,,,, СтрокаСоедиенияСохраняемая);
КонецПроцедуры

Процедура ПроверочнаяСтрокаСоединенияНачалоВыбораИзСписка(Элемент, СтандартнаяОбработка)
	ирОбщий.ПолеВводаСИсториейВыбора_НачалоВыбораИзСпискаЛкс(Элемент, ЭтаФорма);
КонецПроцедуры

Процедура ПроверочнаяСтрокаСоединенияОчистка(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ЭтаФорма.ПроверочнаяСтрокаСоединения = ПроверочнаяСтрокаСоединенияПоУмолчанию();
КонецПроцедуры

Функция ПроверочнаяСтрокаСоединенияПоУмолчанию()
	
	Возврат СтрокаСоединенияИнформационнойБазы() + "Usr=""" + ИмяПользователя() + """;Pwd="""";";

КонецФункции

Процедура КоманднаяПанельКлассыКонсольКода(Кнопка)
	Текст = 
	"ПроверочныйОбъект = Новый COMОбъект(""" + ЭлементыФормы.Классы.ТекущаяСтрока.ИмяКласса + """);
	|Результат = ПроверочныйОбъект.Connect(ПроверочнаяСтрокаСоединения);";
	ПараметрыКода = Новый Структура("ПроверочнаяСтрокаСоединения", ПроверочнаяСтрокаСоединения);
	ирОбщий.ОперироватьСтруктуройЛкс(Текст,, ПараметрыКода);

КонецПроцедуры

Процедура КоманднаяПанельКлассыУстановитьНовойИспользуемую(Кнопка)
	
	Отбор = Новый Структура("СборкаПлатформы, X64");
	Отбор.x64 = x64Текущая;
	Отбор.СборкаПлатформы = ТекущаяСборкаПлатформы;
	УстановитьНовыеСборкиПоСтрокеДоступнойСборки(СборкиПлатформы.НайтиСтроки(Отбор)[0]);
	
КонецПроцедуры

Функция ТекущееИзданиеПлатформы()
	
	Возврат Лев(ирКэш.НомерИзданияПлатформыЛкс(), 1) + "." + Сред(ирКэш.НомерИзданияПлатформыЛкс(), 2);

КонецФункции

ирОбщий.ИнициализироватьФормуЛкс(ЭтаФорма, "Обработка.ирУправлениеCOMКлассами1С.Форма.Форма");
