Функция СохраняемаяНастройкаФормы(выхНаименование, выхИменаСвойств) Экспорт 
	выхИменаСвойств = "Форма.ИспользоватьAutomation, Форма.ВременноПредоставитьПравоРазработчикИР, Форма.ДополнительныеПараметры, Форма.РежимЗапуска, Форма.ОчисткаКэшаКлиентСерверныхВызовов,
	|Форма.КодРазрешения, Форма.ПодключитьОтладчик, Форма.СообщитьКоманднуюСтроку, Форма.ОткрытьПортативныеИнструменты, Форма.ПодменитьПарольНаВремяЗапуска, Форма.РежимИнтерфейсаТакси,
	|Форма.РазделениеДанных, Форма.ОтключитьЗащитуОтОпасныхДействийНаВремяЗапуска, Форма.ЗапускОтИмениАдминистратора, Форма.КодЯзыкаСистемы";
	Возврат Неопределено;
КонецФункции

Процедура ОсновныеДействияФормыСохранитьНастройки(Кнопка)
	
	ирОбщий.ВыбратьИСохранитьНастройкуФормыЛкс(ЭтаФорма);
	
КонецПроцедуры

Процедура ОсновныеДействияФормыЗагрузитьНастройки(Кнопка)
	
	ирОбщий.ВыбратьИЗагрузитьНастройкуФормыЛкс(ЭтаФорма);

КонецПроцедуры

Процедура ОсновныеДействияФормыОК(Кнопка)
	
	СтруктураПользователя = ирОбщий.УстановитьВременныеСвойстваПользователюИБЛкс(ПользовательИБ, ПодменитьПарольНаВремяЗапуска, ВременноПредоставитьПравоРазработчикИР,
		ОтключитьЗащитуОтОпасныхДействийНаВремяЗапуска);
	Если СтруктураПользователя = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Если ИспользоватьAutomation Тогда
		ТипComОбъекта = "Application";
	Иначе
		ТипComОбъекта = "";
	КонецЕсли; 
	Попытка
		ирОбщий.ЗапуститьСеансПодПользователемЛкс(СтруктураПользователя.Имя, СтруктураПользователя.Пароль, ТипComОбъекта, РежимЗапуска, КодРазрешения, СообщитьКоманднуюСтроку,
			ОткрытьПортативныеИнструменты, ОчисткаКэшаКлиентСерверныхВызовов, ПодключитьОтладчик, ДополнительныеПараметры, РежимИнтерфейсаТакси, ЗапускОтИмениАдминистратора,
			РазделениеДанных, Ложь, КодЯзыкаСистемы);
	Исключение
		ирОбщий.ВернутьПостоянныеСвойстваПользователюИБЛкс(СтруктураПользователя);
		ВызватьИсключение;
	КонецПопытки;
	ирОбщий.ВернутьПостоянныеСвойстваПользователюИБЛкс(СтруктураПользователя);

КонецПроцедуры

Процедура ПриОткрытии()
	
	ирОбщий.Форма_ПриОткрытииЛкс(ЭтаФорма);
	ирОбщий.СоздатьМенеджерСохраненияНастроекФормыЛкс(ЭтаФорма);
	ЭлементыФормы.ИспользоватьAutomation.Доступность = ПравоДоступа("Automation", Метаданные, ПользовательИБ);
	Если Не ЭлементыФормы.ИспользоватьAutomation.Доступность Тогда
		ЭтаФорма.ИспользоватьAutomation = Ложь;
	КонецЕсли; 
	ЭлементыФормы.РежимИнтерфейсаТакси.Доступность = Истина
		И ирКэш.НомерИзданияПлатформыЛкс() >= "83"
		И Метаданные.РежимСовместимостиИнтерфейса = Метаданные.СвойстваОбъектов.РежимСовместимостиИнтерфейса.Версия8_2РазрешитьТакси;
	Если Не ЭлементыФормы.РежимИнтерфейсаТакси.Доступность Тогда
		ЭтаФорма.РежимИнтерфейсаТакси = Ложь;
	КонецЕсли; 
	Если ПользовательИБ <> Неопределено Тогда
		Заголовок = Заголовок + " - " + ПользовательИБ.Имя;
		//Если ПользовательИБ.РежимЗапуска = РежимЗапускаКлиентскогоПриложения.ОбычноеПриложение Тогда
		//	ЭтаФорма.РежимЗапуска = "ОбычноеПриложение";
		//ИначеЕсли ПользовательИБ.РежимЗапуска = РежимЗапускаКлиентскогоПриложения.УправляемоеПриложение Тогда
		//	ЭтаФорма.РежимЗапуска = "УправляемоеПриложениеТолстый";
		//КонецЕсли; 
	КонецЕсли; 
	
КонецПроцедуры

Процедура ДополнительныеПараметрыНачалоВыбораИзСписка(Элемент, СтандартнаяОбработка)
	
	ирОбщий.ПолеВводаСИсториейВыбора_НачалоВыбораИзСпискаЛкс(Элемент, ЭтаФорма);

КонецПроцедуры

Процедура ДополнительныеПараметрыПриИзменении(Элемент)
	
	ирОбщий.ПолеВводаСИсториейВыбора_ПриИзмененииЛкс(Элемент, ЭтаФорма);

КонецПроцедуры

Процедура ИспользоватьAutomationПриИзменении(Элемент)
	
	ОбновитьДоступность();
	
КонецПроцедуры

Процедура ОбновитьДоступность()
	
	ЭлементыФормы.ДополнительныеПараметры.Доступность = Не ИспользоватьAutomation;
	ЭлементыФормы.ЗапускОтИмениАдминистратора.Доступность = Не ИспользоватьAutomation;
	
КонецПроцедуры

Процедура ПриЗакрытии()
	
	ирОбщий.Форма_ПриЗакрытииЛкс(ЭтаФорма);

КонецПроцедуры

Процедура ВнешнееСобытие(Источник, Событие, Данные)
	
	ирОбщий.Форма_ВнешнееСобытиеЛкс(ЭтаФорма, Источник, Событие, Данные);

КонецПроцедуры

ирОбщий.ИнициализироватьФормуЛкс(ЭтаФорма, "Обработка.ирРедакторПользователей.Форма.ЗапускПодПользователем");
ЭтаФорма.ПодключитьОтладчик = Истина;
ЭтаФорма.ПодменитьПарольНаВремяЗапуска = Истина;
ЭтаФорма.ОтключитьЗащитуОтОпасныхДействийНаВремяЗапуска = Истина;
ЭтаФорма.ВременноПредоставитьПравоРазработчикИР = Не ирКэш.ЛиПортативныйРежимЛкс();
ЭтаФорма.ОткрытьПортативныеИнструменты = ирКэш.ЛиПортативныйРежимЛкс();
ЭлементыФормы.ВременноПредоставитьПравоРазработчикИР.Доступность = Не ирКэш.ЛиПортативныйРежимЛкс();
ЭлементыФормы.ОткрытьПортативныеИнструменты.Доступность = ирКэш.ЛиПортативныйРежимЛкс();