// http://www.hostedredmine.com/issues/882395

&НаКлиенте
Функция ВывестиСообщение(ТекстСообщения) Экспорт 
	Если Не ЗначениеЗаполнено(ДатаНачалаПотока) Тогда
		УстановитьДатуНачалаПотока();
		ПодключитьОбработчикОжидания("УстановитьДатуНачалаПотока", 1);
	КонецЕсли; 
	Если ТекущаяДата() - ДатаНачалаПотока > 2 Тогда
		Если Не Открыта() Тогда
			Открыть();
		КонецЕсли; 
		Активизировать();
	КонецЕсли; 
	ПодключитьОбработчикОжидания("ЗавершитьВывод", 0.1, Истина);
	Элементы.ГруппаОсновная.Видимость = Истина;
	НовыйТекст = ТекстСообщения;
	Если ЗначениеЗаполнено(Текст) Тогда
		НовыйТекст = Текст + Символы.ПС + НовыйТекст;
	КонецЕсли; 
	ТекстовыйДокумент = Новый ТекстовыйДокумент;
	ТекстовыйДокумент.УстановитьТекст(НовыйТекст);
	СделанаОбрезка = Ложь;
	Пока ТекстовыйДокумент.КоличествоСтрок() > 10 Цикл
		ТекстовыйДокумент.УдалитьСтроку(1);
		СделанаОбрезка = Истина;
	КонецЦикла; 
	Если СделанаОбрезка Тогда
		ТекстовыйДокумент.ВставитьСтроку(1, "...");
	КонецЕсли; 
	ЭтаФорма.Текст = ТекстовыйДокумент.ПолучитьТекст();
	НомерСтроки = ТекстовыйДокумент.КоличествоСтрок();
	Если ТекущаяДата() - ПоследнееОбновлениеОтображения > 0 Тогда
		Элементы.Текст.Видимость = Ложь;
		Элементы.Текст.Видимость = Истина;
		ЭтаФорма.ПоследнееОбновлениеОтображения = ТекущаяДата();
		Элементы.Текст.УстановитьГраницыВыделения(НомерСтроки, 1, НомерСтроки, 1);
	КонецЕсли;
КонецФункции

&НаКлиенте
Процедура УстановитьДатуНачалаПотока() Экспорт 
	ЭтаФорма.ДатаНачалаПотока = ТекущаяДата();
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьВывод() Экспорт 
	Элементы.ГруппаОсновная.Видимость = Ложь;
	ЭтаФорма.Текст = "";
	УстановитьДатуНачалаПотока();
КонецПроцедуры

#Если ТолстыйКлиентУправляемоеПриложение Тогда
//УстановитьДатуНачалаПотока();
//ПодключитьОбработчикОжидания("УстановитьДатуНачалаПотока", 1); // https://partners.v8.1c.ru/forum/t/1930402/m/1930402, http://www.hostedredmine.com/issues/882422
#КонецЕсли 
