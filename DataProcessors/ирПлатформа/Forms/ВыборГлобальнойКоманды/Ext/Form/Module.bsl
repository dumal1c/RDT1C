Процедура ПриОткрытии()
	
	ОбновитьДоступныеКоманды();
	ВыбраннаяГлобальнаяКоманда = ирОбщий.ВосстановитьЗначениеЛкс("ВыбраннаяГлобальнаяКоманда");
	Если ВыбраннаяГлобальнаяКоманда <> Неопределено Тогда
		НовыйТекущийЭлемент = Команды.Найти(ВыбраннаяГлобальнаяКоманда, "Имя");
		Если НовыйТекущийЭлемент <> Неопределено Тогда
			ЭлементыФормы.Команды.ТекущаяСтрока = НовыйТекущийЭлемент;
		КонецЕсли; 
	КонецЕсли; 
	
КонецПроцедуры

Процедура ОбновитьДоступныеКоманды()
	
	Если ЭлементыФормы.Команды.ТекущаяСтрока <> Неопределено Тогда
		СтараяТекущаяКоманда = ЭлементыФормы.Команды.ТекущаяСтрока.Имя;
	КонецЕсли; 
	Команды.Очистить();
	ОтложенноеВыполнение = Ложь;
	АктивнаяФорма = ирОбщий.РодительЭлементаУправляемойФормыЛкс(ТекущийЭлементАктивнойФормы);
	Если ТипЗнч(ТекущийЭлементАктивнойФормы) <> Тип("УправляемаяФорма") Тогда
		ДанныеТекущегоЭлемента = ирОбщий.ДанныеЭлементаФормыЛкс(ТекущийЭлементАктивнойФормы);
	КонецЕсли;
	КлючТекущегоПоляЯчейки = Неопределено;
	ЕстьСсылкиВТекущемПоле = ирОбщий.СсылкиИлиКлючиЗаписейИзТекущегоПоляУправляемойФормыЛкс(АктивнаяФорма, КлючТекущегоПоляЯчейки, Ложь).Количество() > 0;
	КлючТекущейСтрокиТаблицы = Неопределено;
	ирОбщий.КлючиСтрокБДИзТаблицыФормыЛкс(АктивнаяФорма, КлючТекущейСтрокиТаблицы,, Ложь);
	Если КлючТекущегоПоляЯчейки <> Неопределено Тогда
		Если Ложь
			Или ТипЗнч(ТекущийЭлементАктивнойФормы) = Тип("ТаблицаФормы") 
			Или (Истина
				И ТипЗнч(ТекущийЭлементАктивнойФормы) = Тип("ПолеФормы") 
				И (Ложь
					Или ТекущийЭлементАктивнойФормы.Вид = ВидПоляФормы.ПолеВвода
					Или ТекущийЭлементАктивнойФормы.Вид = ВидПоляФормы.ПолеТабличногоДокумента))
		Тогда
			Если ЕстьСсылкиВТекущемПоле Тогда
				Если Истина
					И ТипЗнч(ТекущийЭлементАктивнойФормы) = Тип("ПолеФормы") 
					И ТекущийЭлементАктивнойФормы.Вид = ВидПоляФормы.ПолеВвода
				Тогда
					Параметр = "поле";
				Иначе
					Параметр = "ячейка";
				КонецЕсли; 
				#Если Сервер И Не Сервер Тогда
					РедактироватьОбъектТекущегоПоляАктивнойФормыЛкс();
				#КонецЕсли
				НоваяКоманда = Команды.Добавить();
				НоваяКоманда.Имя = "РедактироватьОбъектТекущегоПоляАктивнойФормыЛкс";
				НоваяКоманда.Представление = "Редактировать объект";
				НоваяКоманда.Пояснение = "Открыть объект по ссылке в редакторе объекта БД (ИР)";
				НоваяКоманда.Картинка = ирКэш.КартинкаИнструментаЛкс("Обработка.ирРедакторОбъектаБД");
				НоваяКоманда.Порядок = 10;
				НоваяКоманда.Параметр = Параметр;
				
				#Если Сервер И Не Сервер Тогда
					ОткрытьОбъектТекущегоПоляАктивнойФормыЛкс();
				#КонецЕсли
				НоваяКоманда = Команды.Добавить();
				НоваяКоманда.Имя = "ОткрытьОбъектТекущегоПоляАктивнойФормыЛкс";
				НоваяКоманда.Представление = "Открыть объект";
				НоваяКоманда.Пояснение = "Открыть объект по ссылке в основной форме";
				НоваяКоманда.Картинка = ирКэш.КартинкаПоИмениЛкс("Лупа");
				НоваяКоманда.Порядок = 20;
				НоваяКоманда.Параметр = Параметр;
			КонецЕсли; 
		КонецЕсли;
	КонецЕсли; 
	Если ЕстьСсылкиВТекущемПоле Тогда
		Если Ложь
			Или ТипЗнч(ТекущийЭлементАктивнойФормы) = Тип("ТаблицаФормы") 
			Или (Истина
				И ТипЗнч(ТекущийЭлементАктивнойФормы) = Тип("ПолеФормы") 
				И (Ложь
					//Или ТекущийЭлементАктивнойФормы.Вид = ВидПоляФормы.ПолеВвода
					Или ТекущийЭлементАктивнойФормы.Вид = ВидПоляФормы.ПолеТабличногоДокумента))
		Тогда
			//Если Истина
			//	И ТипЗнч(ТекущийЭлементАктивнойФормы) = Тип("ПолеФормы") 
			//	И ТекущийЭлементАктивнойФормы.Вид = ВидПоляФормы.ПолеТабличногоДокумента
			//Тогда
				Параметр = "ячейка";
			//Иначе
			//	Параметр = "поле";
			//КонецЕсли; 
			#Если Сервер И Не Сервер Тогда
				ОбработатьОбъектыТекущегоПоляАктивнойФормыЛкс();
			#КонецЕсли
			НоваяКоманда = Команды.Добавить();
			НоваяКоманда.Имя = "ОбработатьОбъектыТекущегоПоляАктивнойФормыЛкс";
			НоваяКоманда.Представление = "Обработать объекты";
			НоваяКоманда.Пояснение = "Обработать выделенные объекты в подборе и обработке объектов БД (ИР)";
			НоваяКоманда.Картинка = ирКэш.КартинкаИнструментаЛкс("Обработка.ирПодборИОбработкаОбъектов");
			НоваяКоманда.Параметр = Параметр;
			НоваяКоманда.Порядок = 30;
		КонецЕсли; 
	КонецЕсли; 
	Если КлючТекущейСтрокиТаблицы <> Неопределено Тогда
		Если ТипЗнч(ТекущийЭлементАктивнойФормы) = Тип("ТаблицаФормы") Тогда
			#Если Сервер И Не Сервер Тогда
				РедактироватьОбъектСтрокиАктивнойФормыЛкс();
			#КонецЕсли
			НоваяКоманда = Команды.Добавить();
			НоваяКоманда.Имя = "РедактироватьОбъектСтрокиАктивнойФормыЛкс";
			НоваяКоманда.Представление = "Редактировать объект";
			НоваяКоманда.Пояснение = "Открыть объект по ссылке в редакторе объекта БД (ИР)";
			НоваяКоманда.Картинка = ирКэш.КартинкаИнструментаЛкс("Обработка.ирРедакторОбъектаБД");
			НоваяКоманда.Параметр = "строка";
			НоваяКоманда.Порядок = 100;
			
			#Если Сервер И Не Сервер Тогда
				ОбработатьОбъектыАктивнойФормыЛкс();
			#КонецЕсли
			НоваяКоманда = Команды.Добавить();
			НоваяКоманда.Имя = "ОбработатьОбъектыАктивнойФормыЛкс";
			НоваяКоманда.Представление = "Обработать объекты";
			НоваяКоманда.Пояснение = "Обработать выделенные/отобранные объекты в подборе и обработке объектов БД (ИР)";
			НоваяКоманда.Картинка = ирКэш.КартинкаИнструментаЛкс("Обработка.ирПодборИОбработкаОбъектов");
			НоваяКоманда.Параметр = "строка";
			НоваяКоманда.Порядок = 110;
		КонецЕсли; 
	КонецЕсли; 
	Если Ложь
		Или ТипЗнч(ТекущийЭлементАктивнойФормы) = Тип("ТаблицаФормы") 
		Или (Истина
			И ТипЗнч(ТекущийЭлементАктивнойФормы) = Тип("ПолеФормы") 
			И ТекущийЭлементАктивнойФормы.Вид = ВидПоляФормы.ПолеТабличногоДокумента)
	Тогда
		Если ТипЗнч(ТекущийЭлементАктивнойФормы) = Тип("ТаблицаФормы") Тогда
			НаименованиеКоманды = "Различные значения колонки";
			Если ДанныеТекущегоЭлемента = Неопределено Тогда
				НаименованиеКоманды = НаименованиеКоманды + " выделенных строк";
			КонецЕсли; 
			#Если Сервер И Не Сервер Тогда
				ОткрытьРазличныеЗначенияКолонкиАктивнойФормыЛкс();
			#КонецЕсли
			НоваяКоманда = Команды.Добавить();
			НоваяКоманда.Имя = "ОткрытьРазличныеЗначенияКолонкиАктивнойФормыЛкс";
			НоваяКоманда.Представление = НаименованиеКоманды;
			НоваяКоманда.Пояснение = "Открыть список различных значений колонки";
			НоваяКоманда.Картинка = ирКэш.КартинкаИнструментаЛкс("Обработка.ирРазличныеЗначенияКолонки");
			НоваяКоманда.Параметр = "таблица";
			НоваяКоманда.Порядок = 200;
		КонецЕсли; 
		
		НаименованиеКоманды = "Сравнить данные";
		Если ТипЗнч(ТекущийЭлементАктивнойФормы) = Тип("ТаблицаФормы") Тогда
			Если ДанныеТекущегоЭлемента = Неопределено Тогда
				НаименованиеКоманды = НаименованиеКоманды + " выделенных строк";
			КонецЕсли; 
			Параметр = "таблица";
		Иначе
			Параметр = "табличный документ";
		КонецЕсли; 
		#Если Сервер И Не Сервер Тогда
			СравнитьТаблицуАктивнойФормыЛкс();
		#КонецЕсли
		НоваяКоманда = Команды.Добавить();
		НоваяКоманда.Имя = "СравнитьТаблицуАктивнойФормыЛкс";
		НоваяКоманда.Представление = НаименованиеКоманды;
		НоваяКоманда.Пояснение = "Передать данные в одну сторону сравнения. Если вторая сторона заполнена, то будет выполнено сравнение.";
		НоваяКоманда.Картинка = ирКэш.КартинкаПоИмениЛкс("ирСравнить");
		НоваяКоманда.Параметр = Параметр;
		НоваяКоманда.Порядок = 210;
		
		НаименованиеКоманды = "Открыть таблицу";
		Параметр = "таблица";
		Если ДанныеТекущегоЭлемента = Неопределено Тогда
			НаименованиеКоманды = НаименованиеКоманды + " выделенных строк";
		ИначеЕсли ТипЗнч(ТекущийЭлементАктивнойФормы) = Тип("ПолеФормы") Тогда
			НаименованиеКоманды = НаименованиеКоманды + " выделенных ячеек";
			Параметр = "табличный документ";
		КонецЕсли; 
		#Если Сервер И Не Сервер Тогда
			ОткрытьТаблицуАктивнойФормыЛкс();
		#КонецЕсли
		НоваяКоманда = Команды.Добавить();
		НоваяКоманда.Имя = "ОткрытьТаблицуАктивнойФормыЛкс";
		НоваяКоманда.Представление = НаименованиеКоманды;
		НоваяКоманда.Пояснение = "Открыть таблицу в редакторе таблицы значений (ИР)";
		НоваяКоманда.Картинка = ирКэш.КартинкаПоИмениЛкс("ирТаблицаЗначений");
		НоваяКоманда.Параметр = Параметр;
		НоваяКоманда.Порядок = 220;
		
		Если ТипЗнч(ТекущийЭлементАктивнойФормы) = Тип("ПолеФормы") Тогда
			#Если Сервер И Не Сервер Тогда
				ОткрытьТабличныйДокументАктивнойФормыЛкс();
			#КонецЕсли
			НоваяКоманда = Команды.Добавить();
			НоваяКоманда.Имя = "ОткрытьТабличныйДокументАктивнойФормыЛкс";
			НоваяКоманда.Представление = "Открыть табличный документ";
			НоваяКоманда.Пояснение = "Открыть табличный документ в редакторе табличного документа (ИР)";
			НоваяКоманда.Картинка = ирКэш.КартинкаПоИмениЛкс("ирТабличныйДокумент");
			НоваяКоманда.Параметр = "табличный документ";
			НоваяКоманда.Порядок = 225;
		КонецЕсли; 
	
		Если ТипЗнч(ДанныеТекущегоЭлемента) = Тип("ДинамическийСписок") Тогда
			#Если Сервер И Не Сервер Тогда
				НастроитьДинамическийСписокАктивнойФормыЛкс();
			#КонецЕсли
			НоваяКоманда = Команды.Добавить();
			НоваяКоманда.Имя = "НастроитьДинамическийСписокАктивнойФормыЛкс";
			НоваяКоманда.Представление = "Настроить список";
			НоваяКоманда.Пояснение = "Редактировать пользовательские и посмотреть фиксированные настройки списка";
			НоваяКоманда.Картинка = ирКэш.КартинкаПоИмениЛкс("НастроитьСписок");
			НоваяКоманда.Параметр = "динамический список";
			НоваяКоманда.Порядок = 230;
			#Если Сервер И Не Сервер Тогда
				ОтборБезЗначенияВТекущейКолонкеАктивнойФормыЛкс();
			#КонецЕсли
			НоваяКоманда = Команды.Добавить();
			НоваяКоманда.Имя = "ОтборБезЗначенияВТекущейКолонкеАктивнойФормыЛкс";
			НоваяКоманда.Представление = "Отбор без значения";
			НоваяКоманда.Пояснение = "Отбор без значения в текущей колонке";
			НоваяКоманда.Картинка = ирКэш.КартинкаПоИмениЛкс("ирОтборБезЗначения");
			НоваяКоманда.Параметр = "динамический список";
			НоваяКоманда.Порядок = 240;
			Если ирОбщий.ЛиКорневойТипСсылочногоОбъектаБДЛкс(ирОбщий.ПервыйФрагментЛкс(ирОбщий.ИмяТаблицыБДДинамическогоСпискаЛкс(ТекущийЭлементАктивнойФормы))) Тогда
				#Если Сервер И Не Сервер Тогда
					НайтиВыбратьСсылкуВДинамическомСпискеАктивнойФормыЛкс();
				#КонецЕсли
				НоваяКоманда = Команды.Добавить();
				НоваяКоманда.Имя = "НайтиВыбратьСсылкуВДинамическомСпискеАктивнойФормыЛкс";
				НоваяКоманда.Представление = "Найти/Выбрать ссылку по ID";
				НоваяКоманда.Пояснение = "";
				НоваяКоманда.Картинка = ирКэш.КартинкаПоИмениЛкс("ирИдентификатор");
				НоваяКоманда.Параметр = "динамический список";
				НоваяКоманда.Порядок = 250;
			КонецЕсли; 
		КонецЕсли; 
	КонецЕсли; 
	Если ирОбщий.КлючОсновногоОбъектаУправляемойФормыЛкс(АктивнаяФорма) <> Неопределено Тогда
		#Если Сервер И Не Сервер Тогда
			РедактироватьОбъектАктивнойФормыЛкс();
		#КонецЕсли
		НоваяКоманда = Команды.Добавить();
		НоваяКоманда.Имя = "РедактироватьОбъектАктивнойФормыЛкс";
		НоваяКоманда.Представление = "Редактировать объект";
		НоваяКоманда.Пояснение = "Открыть объект по ссылке в редакторе объекта БД (ИР)";
		НоваяКоманда.Картинка = ирКэш.КартинкаИнструментаЛкс("Обработка.ирРедакторОбъектаБД");
		НоваяКоманда.Параметр = "форма";
		НоваяКоманда.Порядок = 300;
	КонецЕсли; 
	Если ирОбщий.ЭтоУправляемаяФормаОтчетаЛкс(АктивнаяФорма, Истина) Тогда
		#Если Сервер И Не Сервер Тогда
			ОтладитьКомпоновкуДанныхАктивнойФормыЛкс();
		#КонецЕсли
		НоваяКоманда = Команды.Добавить();
		НоваяКоманда.Имя = "ОтладитьКомпоновкуДанныхАктивнойФормыЛкс";
		НоваяКоманда.Представление = "Отладить компоновку данных";
		НоваяКоманда.Пояснение = "Отладить схему и настройки компоновки отчета в консоли компоновки данных (ИР)";
		НоваяКоманда.Картинка = ирКэш.КартинкаПоИмениЛкс("ирОтладка");
		НоваяКоманда.Параметр = "форма отчета";
		НоваяКоманда.Порядок = 310;
	КонецЕсли; 
	#Если Сервер И Не Сервер Тогда
		ОткрытьСтруктуруАктивнойФормыЛкс();
	#КонецЕсли
	НоваяКоманда = Команды.Добавить();
	НоваяКоманда.Имя = "ОткрытьСтруктуруАктивнойФормыЛкс";
	НоваяКоманда.Представление = "Открыть структуру формы";
	НоваяКоманда.Пояснение = "";
	НоваяКоманда.Картинка = ирКэш.КартинкаИнструментаЛкс("Обработка.ирПлатформа.Форма.СтруктураФормы");
	НоваяКоманда.Параметр = "форма";
	НоваяКоманда.Порядок = 320;
	
	Команды.Сортировать("Порядок");
	Если СтараяТекущаяКоманда <> Неопределено Тогда
		НоваяТекущаяКоманда = Команды.Найти(СтараяТекущаяКоманда, "Имя");
	КонецЕсли; 
	Если НоваяТекущаяКоманда = Неопределено Тогда
		НоваяТекущаяКоманда = Команды[0];
	КонецЕсли; 
	ЭлементыФормы.Команды.ТекущаяСтрока = НоваяТекущаяКоманда;

КонецПроцедуры

Процедура ОсновныеДействияФормыОК(Кнопка = Неопределено)
	
	ЗакрытьФорму();
	//Выполнить(ЭлементыФормы.Команды.ТекущаяСтрока.Значение + "()"); // Так окно откроется в режиме блокирования владельца
	ирОбщий.ПодключитьГлобальныйОбработчикОжиданияЛкс(ЭлементыФормы.Команды.ТекущаяСтрока.Имя,, Истина);
	
КонецПроцедуры

Процедура ЗакрытьФорму()
	ВыбраннаяГлобальнаяКоманда = ЭлементыФормы.Команды.ТекущаяСтрока.Имя;
	ирОбщий.СохранитьЗначениеЛкс("ВыбраннаяГлобальнаяКоманда", ВыбраннаяГлобальнаяКоманда);
	Закрыть(ВыбраннаяГлобальнаяКоманда);
КонецПроцедуры

Процедура КомандыВыбор(Элемент, ЭлементСписка)
	
	ОсновныеДействияФормыОК();
	
КонецПроцедуры

Процедура ОтложенноеВыполнениеПриИзменении(Элемент)
	
	ОбновитьДоступныеКоманды();
	
КонецПроцедуры

Процедура ОсновныеДействияФормыВыполнитьОтложенно(Кнопка)
	
	ЗакрытьФорму();
	ирОбщий.ПодключитьГлобальныйОбработчикОжиданияЛкс("ОткрытьВыборГлобальнойКомандыОтложенноЛкс", 4, Истина);

КонецПроцедуры

Процедура КомандыПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)
	
	//ирОбщий.ТабличноеПолеПриВыводеСтрокиЛкс(ЭтаФорма, Элемент, ОформлениеСтроки, ДанныеСтроки);
	ОформлениеСтроки.Ячейки.Параметр.УстановитьКартинку(ДанныеСтроки.Картинка);
	
КонецПроцедуры

Процедура КомандыПриАктивизацииСтроки(Элемент)
	
	//ирОбщий.ТабличноеПолеПриАктивизацииСтрокиЛкс(ЭтаФорма, Элемент);
	
КонецПроцедуры

ирОбщий.ИнициализироватьФормуЛкс(ЭтаФорма, "Обработка.ирПлатформа.Форма.ВыборГлобальнойКоманды", Ложь);
Команды.Колонки.Добавить("Картинка");