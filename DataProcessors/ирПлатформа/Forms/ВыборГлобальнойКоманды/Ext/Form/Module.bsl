Процедура ПриОткрытии()
	
	Команды.Добавить("РедактироватьОбъектИзАктивнойФормыЛкс", "Редактировать объект из активной ФОРМЫ",, ирКэш.КартинкаИнструментаЛкс("Обработка.ирРедакторОбъектаБД"));
	#Если Сервер И Не Сервер Тогда
		РедактироватьОбъектИзАктивнойФормыЛкс();
	#КонецЕсли
	Команды.Добавить("РедактироватьОбъектИзТекущегоПоляАктивнойФормыЛкс", "Редактировать объект из текущего ПОЛЯ",, ирКэш.КартинкаИнструментаЛкс("Обработка.ирРедакторОбъектаБД"));
	#Если Сервер И Не Сервер Тогда
		РедактироватьОбъектИзТекущегоПоляАктивнойФормыЛкс();
	#КонецЕсли
	Команды.Добавить("ОбработатьОбъектыИзАктивнойФормыЛкс", "Обработать объекты из активной формы",, ирКэш.КартинкаИнструментаЛкс("Обработка.ирПодборИОбработкаОбъектов"));
	#Если Сервер И Не Сервер Тогда
		ОбработатьОбъектыИзАктивнойФормыЛкс();
	#КонецЕсли
	Команды.Добавить("ОткрытьСтруктуруАктивнойФормыЛкс", "Открыть структуру активной формы",, ирКэш.КартинкаИнструментаЛкс("Обработка.ирПлатформа.Форма.СтруктураФормы"));
	#Если Сервер И Не Сервер Тогда
		ОткрытьСтруктуруАктивнойФормыЛкс();
	#КонецЕсли
	ЭлементыФормы.Команды.ТекущаяСтрока = Команды[0];
	ВыбраннаяГлобальнаяКоманда = ирОбщий.ВосстановитьЗначениеЛкс("ВыбраннаяГлобальнаяКоманда");
	Если ВыбраннаяГлобальнаяКоманда <> Неопределено Тогда
		НовыйТекущийЭлемент = Команды.НайтиПоЗначению(ВыбраннаяГлобальнаяКоманда);
		Если НовыйТекущийЭлемент <> Неопределено Тогда
			ЭлементыФормы.Команды.ТекущаяСтрока = НовыйТекущийЭлемент;
		КонецЕсли; 
	КонецЕсли; 
	
КонецПроцедуры

Процедура ОсновныеДействияФормыОК(Кнопка = Неопределено)
	
	ЗакрытьФорму();
	//Выполнить(ЭлементыФормы.Команды.ТекущаяСтрока.Значение + "()"); // Так окно откроется в режиме блокирования владельца
	ирОбщий.ПодключитьГлобальныйОбработчикОжиданияЛкс(ЭлементыФормы.Команды.ТекущаяСтрока.Значение,, Истина); 
	
КонецПроцедуры

Процедура ОсновныеДействияФормыВыполнитьОтложенно(Кнопка = Неопределено)
	
	ирОбщий.ПодключитьГлобальныйОбработчикОжиданияЛкс(ЭлементыФормы.Команды.ТекущаяСтрока.Значение, 5, Истина);
	ЗакрытьФорму();

КонецПроцедуры

Процедура ЗакрытьФорму()
	ВыбраннаяГлобальнаяКоманда = ЭлементыФормы.Команды.ТекущаяСтрока.Значение;
	ирОбщий.СохранитьЗначениеЛкс("ВыбраннаяГлобальнаяКоманда", ВыбраннаяГлобальнаяКоманда);
	Закрыть(ВыбраннаяГлобальнаяКоманда);
КонецПроцедуры

Процедура КомандыВыбор(Элемент, ЭлементСписка)
	
	ОсновныеДействияФормыОК();
	
КонецПроцедуры

ирОбщий.ИнициализироватьФормуЛкс(ЭтаФорма, "Обработка.ирПлатформа.Форма.ВыборГлобальнойКоманды", Ложь);

