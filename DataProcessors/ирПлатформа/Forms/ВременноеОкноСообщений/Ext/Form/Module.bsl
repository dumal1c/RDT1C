Перем АктивнаяФорма;

// http://www.hostedredmine.com/issues/882395
Функция ВывестиСообщение(Знач ТекстСообщения) Экспорт 
	ТекущаяАктивнаяФорма = ирОбщий.АктивнаяФормаЛкс();
	Если ТекущаяАктивнаяФорма <> Неопределено И ТекущаяАктивнаяФорма <> ЭтаФорма Тогда
		АктивнаяФорма = ТекущаяАктивнаяФорма;
	КонецЕсли; 
	Открыть();
	ЭлементыФормы.Текст.Видимость = Истина;
	Если ирКэш.НомерВерсииПлатформыЛкс() < 803015 Или мПлатформа.СчетчикМодальныхФорм.Количество() = 0 Тогда
		ПодключитьОбработчикОжидания("ОчиститьТекстОтложенно", 0.1, Истина); // С этой строкой не работает прикрепление окна
	КонецЕсли; 
	ТекстовыйДокумент = ЭлементыФормы.Текст;
	НовыйТекст = ТекстСообщения;
	Текст = ТекстовыйДокумент.ПолучитьТекст();
	Если ЗначениеЗаполнено(Текст) Тогда
		НовыйТекст = Текст + Символы.ПС + НовыйТекст;
	КонецЕсли; 
	ТекстовыйДокумент.УстановитьТекст(НовыйТекст);
	СделанаОбрезка = Ложь;
	Пока ТекстовыйДокумент.КоличествоСтрок() > 30 Цикл
		ТекстовыйДокумент.УдалитьСтроку(1);
		СделанаОбрезка = Истина;
	КонецЦикла; 
	Если СделанаОбрезка Тогда
		ТекстовыйДокумент.ВставитьСтроку(1, "...");
	КонецЕсли; 
	НомерСтроки = Макс(1, ТекстовыйДокумент.КоличествоСтрок()); // https://www.hostedredmine.com/issues/891268
	Если ТекущаяДата() - ПоследнееОбновлениеОтображения > 0 Тогда
		ЭтаФорма.ПоследнееОбновлениеОтображения = ТекущаяДата();
		ЭлементыФормы.Текст.УстановитьГраницыВыделения(НомерСтроки, 1, НомерСтроки, 1);
	КонецЕсли; 
	Если АктивнаяФорма <> Неопределено Тогда
		АктивнаяФорма.Открыть();
	КонецЕсли; 
КонецФункции

Процедура ОчиститьТекстОтложенно() Экспорт 
	ЭлементыФормы.Текст.Видимость = Ложь;
	ЭлементыФормы.Текст.УстановитьТекст("");
	Если Открыта() Тогда
		Закрыть();
	КонецЕсли; 
	Если АктивнаяФорма <> Неопределено Тогда
		АктивнаяФорма.Активизировать();
	КонецЕсли; 
КонецПроцедуры

Процедура ПриЗакрытии()
	ЭлементыФормы.Текст.УстановитьТекст("");
	//ирОбщий.Форма_ПриЗакрытииЛкс(ЭтаФорма); //
КонецПроцедуры

Процедура ВнешнееСобытие(Источник, Событие, Данные) Экспорт
	
	ирОбщий.Форма_ВнешнееСобытиеЛкс(ЭтаФорма, Источник, Событие, Данные);

КонецПроцедуры

Процедура ПриОткрытии()
	//ирОбщий.Форма_ПриОткрытииЛкс(ЭтаФорма); // 
	ЭтаФорма.УстановитьПрикрепление = мПлатформа.СчетчикМодальныхФорм.Количество() = 0;
КонецПроцедуры

